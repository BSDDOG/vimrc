""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
"                                                                      "
"                                                                      "
"                            BSDDOG .vimrc                            "
"                    The best vimrc in the Universe                    "
"                                                                      "
"                                                                      "
" Both .vimrc and .exrc config files work on OpenBSD, Windows & Linux. "
"    They work on FreeBSD, NetBSD, Mac and ATARI 2600 too. You bet.    "
"                                                                      "
"                                                                      "
"                      OpenBSD Vim configuration:                      "
"        vimrc works for vim and gvim, and exrc for vi and nvi.        "
"                                                                      "
"                                                                      "
" First, it was OpenBSD only. But I wanted a single config file. I had "
" different .vimrc and .exrc files for different OSes. It wasn't okay! "
" Thus I've mixed them up in one single file. As a result, some of the "
" configurations may not be your cup of tee(1). So, fill free to ditch "
" them out! There are two versions of each files (both vimrc and exrc) "
" with and without comment. I prefer the latter, and to RTFM and STFW. "
"                                                                      "
""""""""""""""""""""""""""""""" Features """""""""""""""""""""""""""""""
"                                                                      "
" vimrc OpenBSD:                                                       "
"   * Indentation is an 8 character tab.                               "
"                                                                      "
" Language swtich (LTR and RTL):                                       "
"   * LTR <-> LTR, e.g. English, Russian, etc.                         "
"   * RTL <-> LTR, e.g. Persian, etc.                                  "
"                                                                      "
" Shortkeys/functions for:                                             "
"   * [F1] Save.                                                       "
"   * [F2] Disabling automatic indentation for external pasting.       "
"   * [F3] LTR language swtich.                                        "
"   * [F4] RTL language swtich.                                        "
"   * [F5] Reset to default, after F6 (OpenBSD), and F7/F8 (FDP).      "
"   * [F6] OpenBSD style(9).                                           "
"   * [F7] FreeBSD FDP: replace spaces with tabs.                      "
"   * [F8] FreeBSD FDP: rewrap paragraphs.                             "
"                                                                      "
" A beautiful Status Bar.                                              "
" CLI and GUI compatible.                                              "
" Windows compatible (gvim).                                           "
"                                                                      "
""""""""""""""""""""""""""""""""" Misc """""""""""""""""""""""""""""""""
"                                                                      "
"    * Custom directories for backup, temporary and runtimes files.    "
"    * Incremental search (rounded & highlighted) and the cindent.     "
"    * Disable beep, visual bell and special characters, e.g. ^I.      "
"    * Syntax highlighting and highlight matching.                     "
"    * Ruler, line number and padding.                                 "
"    * Backspace and "@@@" settings.                                   "
"    * Clipboard, history and undo.                                    "
"    * wildmenu and encoding.                                          "
"    * etc.                                                            "
"                                                                      "
""""""""""""""""""" About Function and Function Keys """""""""""""""""""
"                                                                      "
" * You can change default Function keys, via 'nnoremap' & 'inoremap'. "
" * If you don't need a Function or Functions, just delete them.       "
"                                                                      "
"""""""""""""""" OpenBSD, Linux, FreeBSD, and the rest. """"""""""""""""
"                                                                      "
"    Rename the vimrc and/or exrc and vim to .vimrc, .exrc and .vim    "
"                                                                      "
""""""""""""""""""""""" About Custom Directories """""""""""""""""""""""
"                                                                      "
" I prefer to use:                                                     "
"                                                                      "
" '~/.vim/tmp' for backup.                                             "
" '~/.vim/swp' for swap.                                               "
" '~/.vim/add' for plugins.                                            "
"                                                                      "
" But you can change them, or use the vim defaults.                    "
"                                                                      "
""""""""""""""""""""""""""""" Windows Tips """""""""""""""""""""""""""""
"                                                                      "
"  Rename '.vimrc' to '_vimrc' and copy/move it to the %USERPROFILE%.  "
"                                                                      "
""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""



""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
"                                                                      "
"                                .vimrc                                "
"                                                                      "
""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""



""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" Common settings
""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""

" ENABLE the nocompatible mode.
set nocompatible

" ENABLE Syntax highlighting.
syntax on

" SHOW file name on the title of the terminal.
set title

" SHOW line numbers.
set number

" SET line number padding.
"set numberwidth=4

" Show the ruler
set ruler

" SHOW the current mode.
set showmode

" SHOW command in the last line of the screen.
set showcmd

" Bark, when closing an unsaved file.
set confirm

" You can open another file, while the current one is unsaved.
set hidden

" History
set history=1024

" Undo
set undolevels=1024

" '*' clipboard
set clipboard=unnamed

" ENABLE wildmenu
set wildmenu
set wildmode=list:longest,full

" Improves smoothness of redrawing when there are multiple windows.
set ttyfast

" DO NOT show invisibles! e.g. ^I
set nolist

" DISABLE beeping and visual bell
set visualbell
set t_vb=

" Allow BACKSPACING over autoindent, line breaks, and start of insert
set backspace=indent,eol,start

" SHOW @@@ in the last line if it's truncated (DO NOT hide the line)
set display=truncate

" Keep the cursor centered vertically.
"set scrolloff=999

""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" Tab
""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""

set tabstop=8
set softtabstop=0
set shiftwidth=8


""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" Indentation
""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""

set cindent
set cinoptions=(4200,u4200,+0.5s,*500,:0,t0,U4200
set indentkeys=0{,0},0),:,0#,!^F,o,O,e

""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" Highlight matching
""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""

set noshowmatch
set matchpairs+=<:>
let g:loaded_matchparen=1
let g:is_posix=1

""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" Incremental rounded highlighted searching
""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""

set hlsearch
set incsearch
set ignorecase
set shiftround
highlight search cterm=NONE ctermfg=0 ctermbg=7
call matchadd('ColorColumn', '\%81v', 100)

""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" Encoding
""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""

set encoding=utf-8
set ffs=unix,dos,mac
if v:lang =~ "utf8$" || v:lang =~ "UTF-8$"
	set fileencodings=ucs-bom,utf-8,latin1
endif

""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" Solves MS Windows (Cygwin, Msys2, WSL) auto-INSERT problem in start-up.
""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""

set t_u7=

""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" Backup, temp and runtimes directories
""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""

set backup
set backupdir=~/.vim/tmp
set dir=~/.vim/swp
set runtimepath+=~/.vim/add

""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" FileType Settings
""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""

" DISABLE FileType autocommand
filetype off

" If filetype detection was not switched on yet, it will be as well.
"filetype plugin indent on

""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" Status Bar
""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""

set cmdheight=2
set laststatus=2
if has("statusline")
	set statusline=%1*\%02n\ %t\ %y%w%h%r[%{&ff}]%q%m%=[%.20F]\ %5B\ %5O\ %3l:%-3L\ %3c:%-3v\ %3p%%\ \ %*
	highlight User1 ctermfg=red ctermbg=black guifg=red guibg=black
endif


""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" GUI
""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""

if has("gui_running")
	colorscheme koehler
	set background=dark
	let g:rehash256 = 1
	set t_Co=256
	set cursorline
	set guioptions-=r
	set guioptions-=T
	highlight cursor guifg=black guibg=lightgrey
	highlight search guibg=darkgrey guifg=black
endif

""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" Windows
""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""

if has("win32")
	set ffs=dos,unix,mac
	set guifont=dejavu\ sans\ mono:h12
endif

""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" [F1] shortkey: Save
""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""

nnoremap <F1> :w<CR>
inoremap <F1> <ESC>:w<CR>i

""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" [F2] shortkey: Disabling automatic indentation for external pasting 
""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""

nnoremap <F2> :set invpaste paste?<CR>
inoremap <F2> <C-O>:set invpaste paste?<CR>
set pastetoggle=<F2>

""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" [F3] shortkey: Switch the language EN (LTR) <-> RU (LTR)
""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""

nnoremap <silent> <F3> :<C-U>call FuncLangRU()<CR>
inoremap <silent> <F3> <ESC>:<C-U>call FuncLangRU()<CR>a
let s:LangVarRU=1
function! FuncLangRU()
	if s:LangVarRU
		set keymap=russian-jcukenwin
	else
		set keymap=
	endif
	let s:LangVarRU = !s:LangVarRU
endfunction

""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" [F4] shortkey: Switch the language EN (LTR) <-> FA (RTL)
""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""

nnoremap <silent> <F4> :<C-U>call FuncLangFA()<CR>
inoremap <silent> <F4> <ESC>:<C-U>call FuncLangFA()<CR>a
let s:LangVarFA=1
function! FuncLangFA()
	if s:LangVarFA
		set keymap=persian
		set rl
	else
		set keymap=
		set norl
	endif
	let s:LangVarFA = !s:LangVarFA
endfunction

""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" [F5] shortkey: Reset to defaults, after F6 (OpenBSD), and F7/F8 (FDP)
""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""

nnoremap <silent> <F5> :call FuncReset()<CR>
inoremap <silent> <F5> <ESC>:call FuncReset()<CR>i

function! FuncReset()
	syntax on
	filetype plugin indent on
	set colorcolumn=72
	set textwidth=72
	set shiftwidth=8
	set tabstop=8
	set softtabstop=0
	"set expandtab
	set nolist
	set indentexpr=
	set noautoindent
	set nosmartindent
	set cindent
	set cinoptions=(4200,u4200,+0.5s,*500,:0,t0,U4200
	set indentkeys=0{,0},0),:,0#,!^F,o,O,e
	set backspace=indent,eol,start
	:autocmd! GroupSGML BufNewFile,BufRead *.sgml,*.ent,*.xsl,*.xml
	:autocmd! GroupSGML BufNewFile,BufRead *.[1-9]
endfunction

""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" F6: OpenBSD Style
""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
nnoremap <silent> <F6> :call OpenBSD_Style()<CR>
inoremap <silent> <F6> <ESC>:call OpenBSD_Style()<CR>i

function! IgnoreParenIndent()
	let indent = cindent(v:lnum)
	if indent > 4000
		if cindent(v:lnum - 1) > 4000
			return indent(v:lnum - 1)
		else
			return indent(v:lnum - 1) + 4
		endif
	else
		return (indent)
	endif
endfunction

function! OpenBSD_Style()
	setlocal cindent
	setlocal cinoptions=(4200,u4200,+0.5s,*500,:0,t0,U4200
	setlocal indentexpr=IgnoreParenIndent()
	setlocal indentkeys=0{,0},0),:,0#,!^F,o,O,e
	setlocal noexpandtab
	setlocal shiftwidth=8
	setlocal tabstop=8
	setlocal textwidth=80
	setlocal colorcolumn=80 "-2
	setlocal softtabstop=0
endfunction

""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" FreeBSD FDP, SGML setting
"
" F7: Replace spaces with tabs
" F8: Rewrap paragraphs
"
""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
if has("autocmd")
	augroup GroupSGML
		autocmd BufNewFile,BufRead *.sgml,*.ent,*.xsl,*.xml call Set_SGML()
		autocmd BufNewFile,BufRead *.[1-9] call ShowSpecial()
	augroup END
endif

function! Set_Highlights()
	highlight default link OverLength ErrorMsg
	match OverLength /\%71v.\+/
	return 0
endfunction

function! ShowSpecial()
	setlocal list listchars=tab:>>,trail:*,eol:$
	highlight default link nontext ErrorMsg
	return 0
endfunction

function! Set_SGML()
	setlocal number
	syntax match sgmlSpecial "&[^;]*;"
	setlocal syntax=sgml
	setlocal filetype=xml
	setlocal shiftwidth=2
	setlocal textwidth=70
	setlocal colorcolumn=70 "-2
	setlocal tabstop=8
	setlocal softtabstop=2
	setlocal formatprg="fmt -p"
	setlocal autoindent
	setlocal smartindent
	nnoremap <F8> gqj
	inoremap <F8> <ESC>gqji
	nnoremap <F7> :s/        /\t/<CR>
	inoremap <F7> <ESC>:s/        /\t/<CR>i
	call ShowSpecial()
	call Set_Highlights()
	return 0
endfunction
""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
